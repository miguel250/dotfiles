#!/usr/bin/env zsh

# Find dotfile location
export HOMEBREW_CASK_OPTS='--appdir=/Applications'


# Location of projects
export PROJECTS=~/Projects

if [[ -a ~/.localzshrc ]]
then
  source ~/.localzshrc
fi


typeset -U config_files
config_files=($DOTFILEDIR/**/*.zsh)

# load the path files
for file in ${(M)config_files:#*/path.zsh}
do
  source $file
done

# load everything but the path and completion files
for file in ${${config_files:#*/path.zsh}:#*/completion.zsh}
do
  source $file
done

# initialize autocomplete here, otherwise functions won't be loaded
autoload -U compinit
compinit -u

# load every completion after autocomplete loads
for file in ${(M)config_files:#*/completion.zsh}
do
  source $file
done

unset config_files

autoload -U promptinit; promptinit
prompt pure
