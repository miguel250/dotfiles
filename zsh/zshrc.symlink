#!/usr/bin/env zsh

# Use emacs-like key bindings by defaul
bindkey -e

# save current PATH to be restored during reload
if [[ -z $OLD_PATH_RESTORED ]]; then
  export OLD_PATH_RESTORED=$PATH
fi

export PATH=$OLD_PATH_RESTORED

# Find dotfile location
export HOMEBREW_CASK_OPTS='--appdir=/Applications'

if [[ -a ~/.localzshrc ]]
then
  source ~/.localzshrc
fi

eval "$($DOTFILEDIR/bin/dotfile-env)"

typeset -U config_files
config_files=($DOTFILEDIR/**/*.zsh)

# load the path files
for file in ${(M)config_files:#*/path.zsh}
do
  source $file
done

# load everything but the path and completion files
for file in ${${config_files:#*/path.zsh}:#*/completion.zsh}
do
  source $file
done

# initialize autocomplete here, otherwise functions won't be loaded
autoload -U compinit
compinit -u

# load every completion after autocomplete loads
for file in ${(M)config_files:#*/completion.zsh}
do
  source $file
done

unset config_files

fpath+=~/.zfunc
mkdir -p ~/.zfunc/

autoload -U promptinit; promptinit
prompt pure
prompt_pure_set_title() {}

if [ -z "$TMUX" ] ; then
  local session_name="workspace"
  if tmux has-session -t $session_name 2>/dev/null; then
    tmux -2 attach-session -t $session_name
  else
    tmux -2 new-session -s $session_name
  fi
fi
